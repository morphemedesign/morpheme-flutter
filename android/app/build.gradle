plugins {
    id "com.android.application"
    id "com.google.gms.google-services"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    // id "com.google.firebase.crashlytics"
}

def majorVersion = 1
def minorVersion = 0
def patchVersion = 0
def buildVersion = 1

def minSdkVersion = 24
def targetSdkVersion = 36
def compileSdkVersion = 36
def targetNdkVersion = '27.0.12077973'

def code = majorVersion * 10000 + minorVersion * 1000 + patchVersion * 100 + buildVersion * 1
def name = String.format('%d.%d.%d', majorVersion, minorVersion, patchVersion)

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def dartEnvironmentVariables = []
if (project.hasProperty('dart-defines')) {
    dartEnvironmentVariables = project.property('dart-defines')
        .split(',')
        .collectEntries { entry ->
            def pair = new String(entry.decodeBase64(), 'UTF-8').split('=')
            [(pair.first()): pair.last()]
        }
}

android {
    namespace = "design.morpheme"
    compileSdk = compileSdkVersion
    // ndkVersion = targetNdkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId = dartEnvironmentVariables.ANDROID_APPLICATION_ID
        minSdk = minSdkVersion
        targetSdk = targetSdkVersion
        versionCode = flutter.versionCode // change "flutter.versionCode" to "code" for native version 
        versionName = flutter.versionName // change "flutter.versionName" to "name" for native version
        resValue "string", "app_name", dartEnvironmentVariables.APP_NAME
        multiDexEnabled = true
    }

    signingConfigs {
       release {
           keyAlias keystoreProperties['keyAlias']
           keyPassword keystoreProperties['keyPassword']
           storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
           storePassword keystoreProperties['storePassword']
       }
   }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

flutter {
    source = "../.."
}

dependencies {
    implementation "androidx.multidex:multidex:2.0.1"
}
